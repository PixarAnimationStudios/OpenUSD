-- glslfx version 0.1

//
// Copyright 2016 Pixar
//
// Licensed under the Apache License, Version 2.0 (the "Apache License")
// with the following modification; you may not use this file except in
// compliance with the Apache License and the following modification to it:
// Section 6. Trademarks. is deleted and replaced with:
//
// 6. Trademarks. This License does not grant permission to use the trade
//    names, trademarks, service marks, or product names of the Licensor
//    and its affiliates, except as required to comply with Section 4(c) of
//    the License and to reproduce the content of the NOTICE file.
//
// You may obtain a copy of the Apache License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the Apache License with the above modification is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied. See the Apache License for the specific
// language governing permissions and limitations under the Apache License.
//

--- This is what an import might look like.
--- #import $TOOLS/hdSt/shaders/points.hlslfx

#import $TOOLS/hdSt/shaders/instancing.hlslfx
#import $TOOLS/hdSt/shaders/terminals.hlslfx
#import $TOOLS/hdSt/shaders/pointId.hlslfx

--- --------------------------------------------------------------------------
-- layout Point.Vertex

[
    ["out block", "VertexData", "outData",
        ["vec4", "Peye"]
    ]
]

--- --------------------------------------------------------------------------
-- glsl Point.Vertex

// Fwd declare methods defined in pointId.hlslfx, that are used below.
FORWARD_DECL(int GetPointId());
FORWARD_DECL(void ProcessPointId(int id));

void main(void)
{
    MAT4 transform  = ApplyInstanceTransform(HdGet_transform());
    vec4 pt     = vec4(HdGet_points().xyz, 1);

    outData.Peye = vec4(mul(GetWorldToViewMatrix(), mul(transform, pt)));

    ProcessPrimvarsIn();

    gl_Position = mul(GetProjectionMatrix(), outData.Peye);
    ApplyClipPlanes(outData.Peye);

    // check for primvar 'widths'
#if defined(HD_HAS_widths)
    float pvWidth = HdGet_widths();
#else
    float pvWidth = 1.0;
#endif
    // scale width following prman's behavior
    vec4 w = vec4(mul(transform, mul(pvWidth, normalize(vec4(1,1,1,0)))));
    float width = length(w.xyz);

    // compute screenspace width
    vec4 diameter = mul(GetProjectionMatrix(), vec4(width, 0, outData.Peye.z, 1));
    vec2 viewportScale = GetViewport().zw * 0.5;

    gl_PointSize = clamp(viewportScale.x * diameter.x/diameter.w,
                         HD_GL_POINT_SIZE_MIN, HD_GL_POINT_SIZE_MAX);

    ProcessPointId(GetPointId());
}

--- --------------------------------------------------------------------------
-- layout Point.Fragment

[
    ["in block", "VertexData", "inData",
        ["vec4", "Peye"]
    ]
]

--- --------------------------------------------------------------------------
-- glsl Point.Fragment

void main(void)
{
    vec3 Peye = inData.Peye.xyz / inData.Peye.w;
    // camera facing.
    vec3 Neye = vec3(0, 0, 1);

    vec4 color = vec4(0.5, 0.5, 0.5, 1);
#ifdef HD_HAS_displayColor
    color.rgb = HdGet_displayColor().rgb;
#endif
#ifdef HD_HAS_displayOpacity
    color.a = HdGet_displayOpacity();
#endif

    vec4 patchCoord = vec4(0,0,0,0);
    color = ShadingTerminal(vec4(Peye, 1), Neye, color, patchCoord);

#ifdef HD_MATERIAL_TAG_MASKED   
    if (ShouldDiscardByAlpha(color)) {
        discard;
    }
#endif

    RenderOutput(vec4(Peye, 1), Neye, color, patchCoord);
}
