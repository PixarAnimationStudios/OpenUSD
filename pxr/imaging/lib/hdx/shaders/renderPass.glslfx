-- glslfx version 0.1

//
// Copyright 2016 Pixar
//
// Licensed under the Apache License, Version 2.0 (the "Apache License")
// with the following modification; you may not use this file except in
// compliance with the Apache License and the following modification to it:
// Section 6. Trademarks. is deleted and replaced with:
//
// 6. Trademarks. This License does not grant permission to use the trade
//    names, trademarks, service marks, or product names of the Licensor
//    and its affiliates, except as required to comply with Section 4(c) of
//    the License and to reproduce the content of the NOTICE file.
//
// You may obtain a copy of the Apache License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the Apache License with the above modification is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied. See the Apache License for the specific
// language governing permissions and limitations under the Apache License.
//

--- This is what an import might look like.
--- #import $TOOLS/hdx/shaders/renderPass.glslfx

-- glsl HdxRenderPass.ShouldCullFaceShadows

bool ShouldCullFace(bool frontFacing, bool doubleSided)
{
    const uint HdCullStyleDontCare               = uint(0);
    const uint HdCullStyleNothing                = uint(1);
    const uint HdCullStyleBack                   = uint(2);
    const uint HdCullStyleFront                  = uint(3);
    const uint HdCullStyleBackUnlessDoubleSided  = uint(4);
    const uint HdCullStyleFrontUnlessDoubleSided = uint(5);

#if defined(HD_HAS_cullStyle)
    uint cullStyle = HdGet_cullStyle();
#else
    uint cullStyle = HdCullStyleNothing;
#endif

    // Invert the cullstyle so we can render shadows correctly
    return ((cullStyle == HdCullStyleBack                   && frontFacing) ||
            (cullStyle == HdCullStyleFront                  && !frontFacing) ||
            (cullStyle == HdCullStyleBackUnlessDoubleSided  && frontFacing && !doubleSided) ||
            (cullStyle == HdCullStyleFrontUnlessDoubleSided && !frontFacing && !doubleSided));
}

-- glsl HdxRenderPass.RenderColorShadowsOutput

layout (location = 0) out vec4 colorOut;

vec4 ApplyColorOverrides(vec4 color)
{
    return color;
}

void RenderColor(vec4 color)
{
    colorOut = color;
}

-- glsl HdxRenderPass.RenderColorOutputWithSelection

#if defined(HD_HAS_hdxSelectionBuffer)
// helper methods to decode hdxSelectionBuffer
bool HighlightModeHasSelection(int offset)
{
    return (offset != 0);
}

int GetIndexOffsetForHighlightMode(int mode)
{
    return hdxSelectionBuffer[mode + 1/*[0] holds #modes*/];
}

int GetNumSelectionHighlightModes()
{
    return hdxSelectionBuffer[0];
}
#endif

vec4 ApplySelectionColor(vec4 color)
{
#if defined(HD_HAS_hdxSelectionBuffer)
    const int primId = HdGet_primID();
    const int numSelectionModes = GetNumSelectionHighlightModes();
    // The hdxSelectionBuffer layout is:
    // [# highlight modes] [per-mode offsets] [seloffsets mode0] ... [seloffsets modeM]
    // [-------------  header --------------]
    // See HdxSelectionTracker::GetSelectionOffsetBuffer for an example.
    for (int mode = 0; mode < numSelectionModes; mode++) {
        int modeOffset = GetIndexOffsetForHighlightMode(mode);
        if (!HighlightModeHasSelection(modeOffset)) {
            // highlight mode has no objects (prims/instances/elements)
            continue;
        }

        int smin = hdxSelectionBuffer[modeOffset];
        int smax = hdxSelectionBuffer[modeOffset + 1];
        bool sel = false;
        if (primId >= smin && primId < smax) {
            int v = hdxSelectionBuffer[modeOffset + 2 + primId - smin];
            if (v == 1) {
                sel = true;
            } else if (v == 0) {
                sel = false;
            } else {
                // current prim isn't fully selected. check if any of its
                // instances (or) elements are.

                // offsets stored in the buffer are per-selection mode, so
                // account for the additional level of indirection.
                v = v + modeOffset;

                #if defined(HD_INSTANCER_NUM_LEVELS) && HD_INSTANCER_NUM_LEVELS > 0
                for (int level = 0; level < HD_INSTANCER_NUM_LEVELS; level++) {
                    smin = hdxSelectionBuffer[v];
                    smax = hdxSelectionBuffer[v+1];
                    // allow to highlight in a top-down manner, by flipping level
                    // during iteration.
                    // note: instanceIndex[0] is instanceID.
                    //       instanceIndex for level n is stored at instanceIndex[n+1]
                    int instanceId = GetDrawingCoord().instanceIndex[HD_INSTANCER_NUM_LEVELS-level];
                    if (instanceId >= smin && instanceId < smax) {
                        v = hdxSelectionBuffer[2+v+instanceId-smin];
                        if (v == 1) {
                           sel = true;
                           break;
                        } else if (v == 0) {
                           sel = false;
                           break;
                        } else {
                            // instance isn't selected. check if the current
                            // element (face/curve) is.
                            // Account for the mode offset when using 'v' since
                            // it was reset above
                            v = v + modeOffset;
                            smin = hdxSelectionBuffer[v];
                            smax = hdxSelectionBuffer[v+1];
                            int elementId = GetElementID();
                            sel = bool(elementId >= smin && elementId < smax);
                            if (sel) {
                                sel = hdxSelectionBuffer[2+v+elementId-smin] != 0;
                            }
                        }
                    } else {
                        sel = false;
                        break;
                    }
                }
                #else
                    // We have no instances, but we may still have faces selected.
                    smin = hdxSelectionBuffer[v];
                    smax = hdxSelectionBuffer[v+1];
                    int elementId = GetElementID();
                    sel = bool(elementId >= smin && elementId < smax);
                    if (sel) {
                        sel = hdxSelectionBuffer[2+v+elementId-smin] != 0;
                    }
                #endif
            }

            if (sel) {
                vec4 s = vec4(0, 0, 0, 0);

                // XXX: Make selection colors an array so we can avoid the
                // branching.
                if (mode == 0)
                    s = HdGet_selColor();
                else if (mode == 1)
                    s = HdGet_selLocateColor();
                else if (mode == 2)
                    s = HdGet_selMaskColor();

                // Note: We blend mask (over) locate (over) select when an
                // object is in all three highlight modes.
                color.rgb = mix(color.rgb, s.rgb, s.a);
            }
        }
    } // for each highlight mode
    
#endif
    return color;
}

vec4 ApplyColorOverrides(vec4 color)
{
#if defined(HD_HAS_scalarOverride)
    // Allow a scalar value to override the color from the surface
    // for previewing heatmaps. This is useful for editing workflows.
#if defined(HD_HAS_scalarOverrideColorRamp)
    int rampCount = constantPrimVars[GetDrawingCoord().constantCoord].
                scalarOverrideColorRamp.length();
    float scalar = HdGet_scalarOverride() * float(rampCount - 1);
    float baseIndex = floor(scalar);
    float nextIndex = min(float(rampCount - 1), baseIndex + 1.0);
    float interp = scalar - baseIndex;
    color = mix(HdGet_scalarOverrideColorRamp(int(baseIndex)),
                HdGet_scalarOverrideColorRamp(int(nextIndex)),
                interp);
#else
    // If no ramp is given just gamma correct the scalar as greyscale.
    color.rgb = vec3(pow(HdGet_scalarOverride(), 2.2));
#endif // HD_HAS_scalarOverrideColorRamp

#elif defined(HD_HAS_overrideColor)
    // Mix in override color
    vec4 overrideColor = HdGet_overrideColor();
    color.rgb = mix(color.rgb, overrideColor.rgb, overrideColor.a);
#endif
    return ApplySelectionColor(color);
}

layout (location = 0) out vec4 colorOut;

void RenderColor(vec4 color)
{
    colorOut = color;
}

-- glsl HdxRenderPass.RenderColorIdOutput

layout (location = 0) out vec4 primIdOut;
layout (location = 1) out vec4 instanceIdOut;
layout (location = 2) out vec4 elementIdOut;

vec4 ApplyColorOverrides(vec4 color)
{
    // Don't apply overrides during ID render pass.
    return color;
}

void RenderColor(vec4 color)
{
    int primId = HdGet_primID();
    primIdOut = vec4(((primId & 0x0000ff) >>  0) / 255.0,
                     ((primId & 0x00ff00) >>  8) / 255.0,
                     ((primId & 0xff0000) >> 16) / 255.0, 0);

    // instanceIndex is a tuple of integers (num nested levels).
    // for picking, we store global instanceId (instanceIndex[0]) in the
    // selection framebuffer and then reconstruct the tuple in postprocess.
    int instanceId = GetDrawingCoord().instanceIndex[0];
    instanceIdOut = vec4(((instanceId & 0x0000ff) >>  0) / 255.0,
                         ((instanceId & 0x00ff00) >>  8) / 255.0,
                         ((instanceId & 0xff0000) >> 16) / 255.0, 0);

    int elementId = GetElementID();
    elementIdOut = vec4(((elementId & 0x0000ff) >>  0) / 255.0,
                        ((elementId & 0x00ff00) >>  8) / 255.0,
                        ((elementId & 0xff0000) >> 16) / 255.0, 0);
}
